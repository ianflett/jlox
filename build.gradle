plugins {
    id 'application'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'idea'
    id 'jacoco'
    id 'jvm-test-suite'
}

group = 'com.github.ianflett.jlox'
version = '0.8.0.1'

apply from: "$rootDir/gradle/details.gradle"
apply from: "$rootDir/gradle/generate-ast.gradle"
apply from: "$rootDir/gradle/manifest.gradle"

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'com.github.stefanbirkner:system-lambda:1.2.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

defineAst {
    definitions = [
        "${project.group}.Expr": [
            'Binary': ['Expr left', 'Token operator', 'Expr right'],
            'Conditional': ['Expr condition', 'Expr thenBranch', 'Expr elseBranch'],
            'Grouping': ['Expr expression'],
            'Literal': ['Object value'],
            'Unary': ['Token operator', 'Expr right']
        ],
        "${project.group}.Stmt": [
            'Expression': ['Expr expression'],
            'Print': ['Expr expression']
        ]
    ]
}

// https://medium.com/@anirudhramesh95/enforcing-formatting-standards-for-your-java-project-using-gradle-2c21172743e5
spotless {
    ratchetFrom 'origin/main'

    format 'misc', {
        target '.gitattributes', '.gitignore', '*.gradle', '*.md'
        endWithNewline()
        indentWithSpaces(4)
        setEncoding('utf-8')
    }

    format 'trimmed', {
        target '.gitattributes', '.gitignore', '*.gradle'
        trimTrailingWhitespace()
    }

    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat('1.20.0').aosp().reflowLongStrings()
        formatAnnotations()
    }
}

testing {
    suites {
        test {
            useJUnitJupiter()
        }
    }
}

getDetails.company = 'Ian Flett'

application {
    mainClass = "${group}.Lox"
}

// https://stackoverflow.com/a/13172566
run {
    standardInput = System.in
}
